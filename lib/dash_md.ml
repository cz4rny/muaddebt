open Format

let ( <?> ) (c : int) (cmp : unit -> int) = if c <> 0 then c else cmp ()
let header = "## Tech Debt Dashboard"

let footer =
  "<sub>_â€” Generated by [MuadÊ¾Debt](https://github.com/cz4rny/muaddebt) \
   ğŸ«_</sub>"

let pp_dashbaord (fmt : Format.formatter) (todos : Todo.t list) =
  Format.pp_set_margin fmt 100_000;
  fprintf fmt "%s@." header;
  fprintf fmt
    {|
> Tech debt is the mind-killer. MuadÊ¾Debt combs your code for TODOs.  
> You must face your debt; resolve it and let it pass.  
> Where the debt has gone, only flow remains.@.|};

  let open Todo in
  let most_urgent_first_todos =
    List.stable_sort
      (fun t1 t2 ->
        Int.compare t2.urgency t1.urgency <?> fun () ->
        String.compare t1.location.file t2.location.file <?> fun () ->
        Int.compare t1.location.line t2.location.line <?> fun () ->
        Int.compare t1.location.column t2.location.column)
      todos
  in
  List.iter
    (fun t ->
      let loc = t.location in
      fprintf fmt "@.  - [%s: %s](%s#L%d)"
        (Marker.to_string t.marker)
        t.msg loc.file loc.line)
    most_urgent_first_todos;

  fprintf fmt "@.@.%s@." footer

let is_header (line : string) : bool =
  String.starts_with ~prefix:header (String.trim line)

let is_footer (line : string) : bool =
  String.starts_with ~prefix:footer (String.trim line)
